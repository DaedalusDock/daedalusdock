/datum/ai_behavior/flock/attack_target
	name = "incapacitating"
	behavior_flags = AI_BEHAVIOR_REQUIRE_MOVEMENT | AI_BEHAVIOR_MOVE_AND_PERFORM
	goap_weight = FLOCK_BEHAVIOR_WEIGHT_SHOOT
	required_distance = 3

	search_radius_override = 12

/datum/ai_behavior/flock/attack_target/setup(datum/ai_controller/controller, mob/overmind_target)
	. = ..()
	if(overmind_target)
		var/mob/living/simple_animal/flock/drone/bird = controller.pawn
		if(!goap_is_valid_target(controller, overmind_target))
			controller.set_blackboard_key(BB_FLOCK_OVERMIND_CONTROL, TRUE)
			controller.set_blackboard_key(BB_PATH_MAX_LENGTH, 200)
			bird.say("instruction confirmed: incapacitate lifeform")
		else
			bird.say("invalid attack target provided by sentient-level instruction")
			return FALSE

	var/atom/target = overmind_target || goap_get_ideal_target(controller, set_path = TRUE)
	if(!target)
		return FALSE

	controller.set_blackboard_key(BB_FLOCK_ATTACK_TARGET, target)
	controller.set_move_target(target)
	controller.queue_behavior(/datum/ai_behavior/frustration, BB_FLOCK_ATTACK_FRUSTRATION, 10 SECONDS)

/datum/ai_behavior/flock/attack_target/goap_precondition(datum/ai_controller/controller)
	var/mob/living/simple_animal/flock/drone/bird = controller.pawn
	return length(bird.flock?.enemies)

/datum/ai_behavior/flock/attack_target/goap_get_potential_targets(datum/ai_controller/controller)
	var/mob/living/simple_animal/flock/bird = controller.pawn
	return bird.flock.enemies.Copy()

/datum/ai_behavior/flock/attack_target/goap_is_valid_target(datum/ai_controller/controller, atom/target)
	var/mob/living/target_mob = target
	return ismob(target_mob) && isturf(target_mob.loc) && !target_mob.incapacitated(IGNORE_STASIS | IGNORE_GRAB | IGNORE_RESTRAINTS) && !target_mob.IsKnockdown()

/datum/ai_behavior/flock/attack_target/perform(delta_time, datum/ai_controller/controller, mob/overmind_target)
	..()
	var/atom/target = controller.blackboard[BB_FLOCK_ATTACK_TARGET]
	if(!target)
		return BEHAVIOR_PERFORM_FAILURE

	if(!goap_is_valid_target(controller, target))
		return BEHAVIOR_PERFORM_FAILURE

	if(!COOLDOWN_FINISHED(controller, blackboard[BB_FLOCK_ATTACK_COOLDOWN]))
		controller.set_blackboard_key(BB_FLOCK_ATTACK_FRUSTRATION, world.time) // Reset frustration if we're on cooldown.
		return BEHAVIOR_PERFORM_COOLDOWN

	var/mob/living/simple_animal/flock/drone/bird = controller.pawn
	if(!can_see(bird, target))
		return BEHAVIOR_PERFORM_COOLDOWN

	if(get_dist(bird, target) > 6) // Too far to shoot!
		return BEHAVIOR_PERFORM_COOLDOWN

	// Run away!
	if(DT_PROB(40, delta_time) && COOLDOWN_FINISHED(controller, blackboard[BB_FLOCK_ATTACK_RUN_COOLDOWN]) && get_dist(bird, target) < 3)
		controller.set_blackboard_key(BB_FLOCK_ATTACK_RUN_COOLDOWN, world.time + 5 SECONDS)
		SSmove_manager.move_away(bird, target, 6, bird.movement_delay, bird.movement_delay * 4, priority = MOVEMENT_DEFAULT_PRIORITY + 1)

	// Strafe!
	else if(DT_PROB(60, delta_time) && COOLDOWN_FINISHED(controller, blackboard[BB_FLOCK_ATTACK_STRAFE_COOLDOWN]))
		controller.set_blackboard_key(BB_FLOCK_ATTACK_STRAFE_COOLDOWN, bird.movement_delay * 3)
		var/step_dir = turn(get_dir(bird, target), prob(50) ? 90 : -90)
		step(bird, step_dir)

	if(!istype(bird.active_part, /datum/flockdrone_part/incapacitator))
		var/datum/flockdrone_part/incapacitator/weapon = locate() in bird.parts
		bird.set_active_part(weapon)

	controller.set_blackboard_key(BB_FLOCK_ATTACK_COOLDOWN, world.time + 1.2 SECONDS)
	controller.set_blackboard_key(BB_FLOCK_ATTACK_FRUSTRATION, world.time) // Reset frustration

	bird.RangedAttack(target)
	return BEHAVIOR_PERFORM_COOLDOWN

/datum/ai_behavior/flock/attack_target/finish_action(datum/ai_controller/controller, succeeded, ...)
	. = ..()
	controller.clear_blackboard_key(BB_FLOCK_ATTACK_TARGET)
	controller.clear_blackboard_key(BB_FLOCK_ATTACK_FRUSTRATION)
