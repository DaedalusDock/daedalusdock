// Watermelon
/datum/plant/watermelon
	species = "watermelon"
	name = "Watermelon"

	growing_icon = 'icons/obj/hydroponics/growing_fruits.dmi'
	icon_dead = "watermelon-dead"

	seed_path = /obj/item/seeds/watermelon
	product_path = /obj/item/food/grown/watermelon

	base_health = 30
	base_maturation = 120
	base_production = 200
	base_harvest_amt = 5
	base_harvest_yield = 2
	base_endurance = 5

	genome = 19

	latent_genes = list(/datum/plant_gene/seedless, /datum/plant_gene/immortal)

	possible_mutations = list(/datum/plant_mutation/watermelon_holy)
	reagents_per_potency= list(/datum/reagent/water = 0.2, /datum/reagent/consumable/nutriment/vitamin = 0.04, /datum/reagent/consumable/nutriment = 0.2)

/obj/item/seeds/watermelon
	name = "pack of watermelon seeds"
	desc = "These seeds grow into watermelon plants."
	icon_state = "seed-watermelon"

	plant_type = /datum/plant/watermelon

/obj/item/seeds/watermelon/suicide_act(mob/living/user)
	user.visible_message(span_suicide("[user] is swallowing [src]! It looks like [user.p_theyre()] trying to commit suicide!"))
	user.gib()
	var/product = plant_datum.product_path
	new product(drop_location())
	qdel(src)
	return MANUAL_SUICIDE

/obj/item/food/grown/watermelon
	plant_datum = /datum/plant/watermelon
	name = "watermelon"
	desc = "It's full of watery goodness."
	icon_state = "watermelon"
	bite_consumption_mod = 2
	w_class = WEIGHT_CLASS_NORMAL
	foodtypes = FRUIT
	juice_results = list(/datum/reagent/consumable/watermelonjuice = 0)
	wine_power = 40

/obj/item/food/grown/watermelon/MakeProcessable()
	AddElement(/datum/element/processable, TOOL_KNIFE, /obj/item/food/watermelonslice, 5, 20)

/obj/item/food/grown/watermelon/make_dryable()
	return //No drying

// Holymelon
/datum/plant_mutation/watermelon_holy
	plant_type = /datum/plant/watermelon/holy

	infusion_reagents = list(/datum/reagent/water/holywater)

/datum/plant/watermelon/holy
	species = "holymelon"
	name = "Holymelon"

	seed_path = /obj/item/seeds/watermelon/holy
	product_path = /obj/item/food/grown/holymelon

	innate_genes = list(/datum/plant_gene/product_trait/anti_magic)
	latent_genes = list(/datum/plant_gene/product_trait/glow/yellow)
	possible_mutations = null

	reagents_per_potency = list(/datum/reagent/water/holywater = 0.2, /datum/reagent/consumable/nutriment/vitamin = 0.04, /datum/reagent/consumable/nutriment = 0.1)
	rarity = 20

/obj/item/seeds/watermelon/holy
	name = "pack of holymelon seeds"
	desc = "These seeds grow into holymelon plants."
	icon_state = "seed-holymelon"

	plant_type = /datum/plant/watermelon/holy

/obj/item/food/grown/holymelon
	plant_datum = /datum/plant/watermelon/holy
	name = "holymelon"
	desc = "The water within this melon has been blessed by some deity that's particularly fond of watermelon."
	icon_state = "holymelon"
	bite_consumption_mod = 2
	wine_power = 70 //Water to wine, baby.
	wine_flavor = "divinity"

/obj/item/food/grown/holymelon/make_dryable()
	return //No drying

/obj/item/food/grown/holymelon/MakeEdible()
	AddComponent(/datum/component/edible, \
		initial_reagents = food_reagents, \
		food_flags = food_flags, \
		foodtypes = foodtypes, \
		volume = max_volume, \
		eat_time = eat_time, \
		tastes = tastes, \
		eatverbs = eatverbs,\
		bite_consumption = bite_consumption, \
		microwaved_type = microwaved_type, \
		junkiness = junkiness, \
		check_liked = CALLBACK(src, PROC_REF(check_holyness)))

/*
 * Callback to be used with the edible component.
 * Checks whether or not the person eating the holymelon
 * is a holy_role (chaplain), as chaplains love holymelons.
 */
/obj/item/food/grown/holymelon/proc/check_holyness(fraction, mob/mob_eating)
	if(!ishuman(mob_eating))
		return
	var/mob/living/carbon/human/holy_person = mob_eating
	if(!holy_person.mind?.holy_role || HAS_TRAIT(holy_person, TRAIT_AGEUSIA))
		return
	to_chat(holy_person, span_notice("Truly, a piece of heaven!"))
	return FOOD_LIKED
